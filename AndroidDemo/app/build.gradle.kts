import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.util.Properties

plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.kotlinAndroid)
    alias(libs.plugins.kotlinCompose)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hiltAndroid)
}

// Force OkHttp version to avoid conflicts between app dependencies and test dependencies
configurations.all {
    resolutionStrategy {
        force("com.squareup.okhttp3:okhttp:5.3.2")
    }
}

fun escapedBuildConfigString(value: String): String {
    val escaped =
        value
            .replace("\\", "\\\\")
            .replace("\"", "\\\"")
    return "\"$escaped\""
}

fun readLocalProperty(
    rootDir: java.io.File,
    name: String,
): String? {
    val localPropertiesFile = rootDir.resolve("local.properties")
    if (!localPropertiesFile.exists()) {
        return null
    }

    val properties = Properties()
    localPropertiesFile.inputStream().use(properties::load)
    return properties.getProperty(name)
}

android {
    namespace = "io.ton.walletkit.demo"
    compileSdk = 36

    defaultConfig {
        applicationId = "io.ton.walletkit.demo"
        minSdk = 26
        targetSdk = 36
        versionCode = (System.getenv("GITHUB_RUN_NUMBER") ?: "1").toInt()
        versionName = findProperty("DEMO_VERSION_NAME") as String? ?: "1.0.0"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        // Pass instrumentation arguments from gradle properties or environment
        // Usage: ./gradlew connectedDebugAndroidTest -PtestMnemonic="word1 word2 ..."
        // Or set TEST_MNEMONIC environment variable
        val testMnemonic =
            findProperty("testMnemonic") as String?
                ?: System.getenv("TEST_MNEMONIC")
        val disableNetworkSend =
            findProperty("disableNetworkSend") as String?
                ?: System.getenv("DISABLE_NETWORK_SEND")
                ?: "true"
        val allureToken =
            findProperty("allureToken") as String?
                ?: System.getenv("ALLURE_API_TOKEN")
        val toncenterApiKey =
            findProperty("walletkitToncenterApiKey") as String?
                ?: readLocalProperty(rootDir, "walletkitToncenterApiKey")
                ?: readLocalProperty(rootDir, "tonCenterApiKey")
                ?: System.getenv("WALLETKIT_TONCENTER_API_KEY")
                ?: System.getenv("TONCENTER_API_KEY")
                ?: ""
        val tonApiKey =
            findProperty("walletkitTonApiKey") as String?
                ?: readLocalProperty(rootDir, "walletkitTonApiKey")
                ?: System.getenv("WALLETKIT_TONAPI_API_KEY")
                ?: ""
        val tonApiMainnetKey =
            readLocalProperty(rootDir, "tonApiMainnetKey")
                ?: System.getenv("MAINNET_API_KEY")
                ?: ""
        val tonApiTestnetKey =
            readLocalProperty(rootDir, "tonApiTestnetKey")
                ?: System.getenv("TESTNET_API_KEY")
                ?: ""
        val tetraApiKey =
            readLocalProperty(rootDir, "tetraApiKey")
                ?: System.getenv("TETRA_API_KEY")
                ?: ""

        testMnemonic?.let {
            testInstrumentationRunnerArguments["testMnemonic"] = it
        }
        testInstrumentationRunnerArguments["disableNetworkSend"] = disableNetworkSend
        allureToken?.let {
            testInstrumentationRunnerArguments["allureToken"] = it
        }

        buildConfigField("String", "TONCENTER_API_KEY", escapedBuildConfigString(toncenterApiKey))
        buildConfigField("String", "TONAPI_API_KEY", escapedBuildConfigString(tonApiKey))
        buildConfigField("String", "MAINNET_API_KEY", escapedBuildConfigString(tonApiMainnetKey))
        buildConfigField("String", "TESTNET_API_KEY", escapedBuildConfigString(tonApiTestnetKey))
        buildConfigField("String", "TETRA_API_KEY", escapedBuildConfigString(tetraApiKey))
    }

    signingConfigs {
        getByName("debug") {
            storeFile = file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            signingConfig = signingConfigs.getByName("debug")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    buildFeatures {
        buildConfig = true
        compose = true
        buildConfig = true
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

dependencies {
    implementation(project(":designsystem"))

    implementation(libs.androidxCoreKtx)
    implementation(libs.androidxAppcompat)
    implementation(libs.googleMaterial)
    implementation(libs.androidxActivityKtx)
    implementation(libs.androidxConstraintLayout)
    implementation(libs.androidxActivityCompose)
    implementation(platform(libs.androidxComposeBom))
    implementation(libs.androidxComposeUi)
    implementation(libs.androidxComposeMaterial3)
    // material-icons-core (~50 icons) replaces material-icons-extended (~11,000 icons,
    // ~40 MB of DEX). The 15 icons the demo uses that aren't in `core` are defined locally
    // in io.ton.walletkit.demo.presentation.ui.icons.DemoIcons.
    implementation(libs.androidxComposeMaterialIconsCore)
    implementation(libs.androidxComposeUiToolingPreview)
    debugImplementation(libs.androidxComposeUiTooling)
    implementation(libs.androidxLifecycleRuntimeKtx)
    implementation(libs.androidxLifecycleViewmodelCompose)
    implementation(libs.kotlinxCoroutinesAndroid)
    implementation(libs.androidxSecurityCrypto)
    implementation(libs.androidxBiometric)
    implementation(libs.coilCompose)
    implementation(libs.coilNetwork)

    // GMS Code Scanner — fully-managed QR scanner UI (runs in Google Play services,
    // no CAMERA permission needed in the app). Used by the dev Investigation screen.
    implementation(libs.playServicesCodeScanner)

    // Hilt dependency injection
    implementation(libs.hiltAndroid)
    ksp(libs.hiltCompiler)

    // TONWalletKit SDK - Use local AAR file
    // Build and copy with: cd ../TONWalletKit-Android && ./gradlew buildAndCopyToDemo
    implementation(files("libs/tonwalletkit-release.aar"))
    // Required transitive dependencies when using AAR:
    implementation(libs.androidxWebkit)
    implementation(libs.androidxDatastorePreferences)

    implementation(libs.kotlinxSerializationJson)

    // Google Tink for X25519 keypair generation (used by TestSessionManager)
    // Tink provides pure Java implementation, no native dependencies
    implementation(libs.tinkAndroid)

    debugImplementation(libs.leakcanaryAndroid)

    // Testing - Unit Tests
    testImplementation(libs.junit)

    // Testing - Instrumentation Tests (Espresso + Compose)
    androidTestImplementation(libs.androidxTestCore)
    androidTestImplementation(libs.androidxTestCoreKtx)
    androidTestImplementation(libs.androidxTestRunner)
    androidTestImplementation(libs.androidxTestRules)
    androidTestImplementation(libs.androidxTestEspressoCore)
    androidTestImplementation(libs.androidxTestEspressoWeb)
    androidTestImplementation(libs.androidxTestEspressoIntents)
    androidTestImplementation(libs.androidxTestUiAutomator)
    androidTestImplementation(platform(libs.androidxComposeBom))
    androidTestImplementation(libs.androidxComposeUiTestJunit4)
    debugImplementation(libs.androidxComposeUiTestManifest)

    // Allure reporting for tests
    androidTestImplementation(libs.allureKotlinAndroid)
    androidTestImplementation(libs.allureKotlinModel)
    androidTestImplementation(libs.allureKotlinCommons)

    // OkHttp for Allure API client
    androidTestImplementation(libs.okhttp)
    androidTestImplementation(libs.kotlinxSerializationJson)

    // Hilt testing
    androidTestImplementation(libs.hiltAndroidTesting)
    kspAndroidTest(libs.hiltCompiler)
}
